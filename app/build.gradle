apply plugin: 'application'
mainClassName = 'myproject.meetup.contentful.productcatalog.Main'

bootJar {
    enabled = true
}

jar {
    enabled = true
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootVersion}"
    compile 'com.contentful.java:java-sdk:10.0.0'
    compile 'com.contentful.java:cma-sdk:3.1.0'
    compile group: 'org.json', name: 'json', version: '20180813'
    compile group: 'org.neo4j.driver', name: 'neo4j-java-driver', version: '1.6.3'
    compile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.4.0'
    testCompile 'org.awaitility:awaitility:3.1.2'
    testCompile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'

}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }
}

springBoot {
    mainClassName = 'myproject.meetup.contentful.productcatalog.Main'
}

build.dependsOn(installDist)

// setup separate integration tests
sourceSets {
    integrationTest {
        java.srcDir file('src/intTest/java')
        resources.srcDir file('src/intTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn integrationTest
