plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id "com.stehno.gradle.dependency-checker" version "0.2.3"
}

group = 'contentful.neo4j'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_12
    targetCompatibility = JavaVersion.VERSION_12
}

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        compileClasspath += sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

bootJar {
    mainClassName = 'myproject.meetup.contentful.productcatalog.Main'
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.contentful.java:java-sdk:10.4.1'
    implementation 'com.contentful.java:cma-sdk:3.3.2'
    implementation 'org.neo4j.driver:neo4j-java-driver:1.6.3'
    implementation 'org.json:json:20190722'
    implementation 'com.jayway.jsonpath:json-path:2.4.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.junit.platform:junit-platform-runner:1.6.0'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
//    testImplementation 'org.mockito:mockito-core:3.2.4'
    testRuntimeOnly 'org.apache.httpcomponents:httpclient:4.5.11'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()

    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest
